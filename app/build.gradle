plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'com.squareup.anvil'
}

android {
    namespace 'com.fret.open_source_imgur'
    compileSdk 33

    Properties localProperties = new Properties()
    localProperties.load(project.rootProject.file('local.properties').newDataInputStream())

    defaultConfig {
        applicationId "com.fret.open_source_imgur"
        minSdk 29
        targetSdk 33
        versionCode 1
        versionName "1.0"

        resConfigs "en", "de"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        
        //open-source-imgur://com.fret.open_source_imgur/list_fragment
        def LOCALHOST_AUTH_REDIRECT = "http://192.168.1.17:8080/"
        def AUTH_REDIRECT = "https://auth.reddigfabian.com"
        def DEEPLINK_SCHEME = "open-source-imgur"
        def DEEPLINK_HOST = "com.fret.open_source_imgur"
        def DEEPLINK_URI = "$DEEPLINK_SCHEME://$DEEPLINK_HOST"
        def DEEPLINK_LIST_FRAGMENT = "/list_fragment"
        def DEEPLINK_REDIRECT_LIST = "$DEEPLINK_URI$DEEPLINK_LIST_FRAGMENT"
        def DEEPLINK_ACCESS_TOKEN_PARAM = "access_token"
        def DEEPLINK_EXPIRES_IN_PARAM = "expires_in"
        def DEEPLINK_REFRESH_TOKEN_PARAM = "refresh_token"
        def DEEPLINK_REDIRECT_WITH_ACCESS_TOKEN_PARAM = "$DEEPLINK_REDIRECT_LIST?$DEEPLINK_ACCESS_TOKEN_PARAM={$DEEPLINK_ACCESS_TOKEN_PARAM}"

        buildConfigField "String", "LOCALHOST_AUTH_REDIRECT", "\"$LOCALHOST_AUTH_REDIRECT\""
        buildConfigField "String", "AUTH_REDIRECT", "\"$AUTH_REDIRECT\""
        buildConfigField "String", "DEEPLINK_REDIRECT_LIST", "\"$DEEPLINK_REDIRECT_LIST\""
        resValue "string", "DEEPLINK_ACCESS_TOKEN_PARAM", DEEPLINK_ACCESS_TOKEN_PARAM
        resValue "string", "DEEPLINK_EXPIRES_IN_PARAM", DEEPLINK_EXPIRES_IN_PARAM
        resValue "string", "DEEPLINK_REFRESH_TOKEN_PARAM", DEEPLINK_REFRESH_TOKEN_PARAM
        resValue "string", "DEEPLINK_REDIRECT_WITH_ACCESS_TOKEN_PARAM", DEEPLINK_REDIRECT_WITH_ACCESS_TOKEN_PARAM

        manifestPlaceholders = [
                appAuthRedirectScheme : DEEPLINK_SCHEME//Used by AppAuth
        ]
    }

    signingConfigs {
        release {
            storeFile file("myreleasekey.keystore")
            storePassword "password"
            keyAlias "MyReleaseKey"
            keyPassword "password"
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            debuggable true
        }
        release {
            minifyEnabled true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    def javaVersion = JavaVersion.VERSION_11
    compileOptions {
        sourceCompatibility javaVersion
        targetCompatibility javaVersion
    }
    kotlinOptions {
        jvmTarget = javaVersion.toString()
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation libs.androidx.core
    implementation libs.androidx.appcompat
    implementation libs.material
    implementation libs.androidx.fragment
    implementation libs.androidx.navigation.fragment

    kapt libs.dagger.compiler

    implementation project(":shared_resources")
    implementation project(":di")
    implementation project(":utils:di")
    implementation project(':shared_menus')
    implementation project(":imgur_gallery:impl")
    implementation project(":imgur_album:impl")
    implementation project(":imgur_api:impl")
    implementation project(":imgur_auth:impl")
    implementation project(":testpackage:impl")

    testImplementation libs.junit
    androidTestImplementation libs.androidx.junit
    androidTestImplementation libs.espresso.core
}