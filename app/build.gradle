plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {
    namespace 'com.fret.grocerydemo'
    compileSdk 32

    Properties localProperties = new Properties()
    localProperties.load(project.rootProject.file('local.properties').newDataInputStream())

    defaultConfig {
        applicationId "com.fret.grocerydemo"
        minSdk 29
        targetSdk 32
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        def KROGER_API_BASE_URL = "https://api.kroger.com/v1/"
        def KROGER_OAUTH2_AUTH_CODE = "${KROGER_API_BASE_URL}connect/oauth2/authorize"
        def KROGER_OAUTH2_ACCESS_TOKEN = "${KROGER_API_BASE_URL}connect/oauth2/token"
        buildConfigField "String", "KROGER_API_BASE_URL", "\"$KROGER_API_BASE_URL\""
        buildConfigField "String", "KROGER_OAUTH2_AUTH_CODE", "\"$KROGER_OAUTH2_AUTH_CODE\""
        buildConfigField "String", "KROGER_OAUTH2_ACCESS_TOKEN", "\"$KROGER_OAUTH2_ACCESS_TOKEN\""

        def DEEPLINK_SCHEME = "grocery-demo"
        def DEEPLINK_HOST = "com.fret.grocerydemo"
        def DEEPLINK_URI = "$DEEPLINK_SCHEME://$DEEPLINK_HOST"
        def DEEPLINK_LIST_FRAGMENT = "/list_fragment"
        def DEEPLINK_REDIRECT = "$DEEPLINK_URI$DEEPLINK_LIST_FRAGMENT"
        def DEEPLINK_ACCESS_CODE_PARAM = "code"
        def DEEPLINK_REDIRECT_WITH_ACCESS_CODE_PARAM = "$DEEPLINK_REDIRECT?$DEEPLINK_ACCESS_CODE_PARAM={$DEEPLINK_ACCESS_CODE_PARAM}"

        buildConfigField "String", "DEEPLINK_REDIRECT", "\"$DEEPLINK_REDIRECT\""
        resValue "string", "DEEPLINK_ACCESS_CODE_PARAM", DEEPLINK_ACCESS_CODE_PARAM
        resValue "string", "DEEPLINK_REDIRECT_WITH_ACCESS_CODE_PARAM", DEEPLINK_REDIRECT_WITH_ACCESS_CODE_PARAM

        manifestPlaceholders = [
                appAuthRedirectScheme : DEEPLINK_SCHEME,//Used by AppAuth
                DEEPLINK_SCHEME : DEEPLINK_SCHEME,
                DEEPLINK_HOST : DEEPLINK_HOST,
                DEEPLINK_LIST_FRAGMENT : DEEPLINK_LIST_FRAGMENT
        ]
    }

    signingConfigs {
        release {
            storeFile file("myreleasekey.keystore")
            storePassword "password"
            keyAlias "MyReleaseKey"
            keyPassword "password"
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            debuggable true
            buildConfigField "String", "KROGER_CLIENT_ID", localProperties.get("kroger.client.id", "")
            buildConfigField "String", "KROGER_CLIENT_SECRET", localProperties.get("kroger.client.secret", "")
        }
        release {
            minifyEnabled true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    def javaVersion = JavaVersion.VERSION_11
    compileOptions {
        sourceCompatibility javaVersion
        targetCompatibility javaVersion
    }
    kotlinOptions {
        jvmTarget = javaVersion.toString()
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation "androidx.fragment:fragment-ktx:1.4.1"
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.6.0'
    implementation "androidx.navigation:navigation-fragment-ktx:2.4.2"
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    implementation "androidx.paging:paging-runtime-ktx:3.1.1"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.1'
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.1"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.4.1"
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.3'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation "com.squareup.retrofit2:converter-moshi:2.9.0"
    implementation "com.squareup.moshi:moshi-kotlin:1.13.0"
    implementation "com.squareup.moshi:moshi-adapters:1.8.0"
    implementation "androidx.datastore:datastore-preferences:1.0.0"
    implementation 'net.openid:appauth:0.11.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}